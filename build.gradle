apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'cpd'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

targetCompatibility = 1.7
sourceCompatibility = 1.7

group 'io.github.raindev'
version '1.0.0'

description 'Java Bencode minimalist library'

ext {
    pmdVersion = '5.3.3'
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'org.testng', name: 'testng', version:'6.9.9'
    testCompile group: 'org.unitils', name: 'unitils-core', version:'3.4.2'
    testCompile group: 'commons-io', name: 'commons-io', version:'2.4'
    pmd(
        'net.sourceforge.pmd:pmd-core:' + pmdVersion,
        'net.sourceforge.pmd:pmd-java:' + pmdVersion
    )
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'de.aaschmid.gradle.plugins:gradle-cpd-plugin:0.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
    }
}

test {
    useTestNG()

    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: " +
                    "${result.testCount} tests," +
                    " ${result.successfulTestCount} successful," +
                    " ${result.failedTestCount} failed," +
                    " ${result.skippedTestCount} skipped"
            }
        }
    }
}

findbugs {
    toolVersion '3.0.1'
    effort 'max'
    reportLevel 'high'
}

pmd {
    toolVersion pmdVersion
    ruleSetFiles = files('pmd.xml', 'pmd-main.xml')
}

cpd {
    toolVersion pmdVersion
}

checkstyle {
    toolVersion '6.10.1'
    configFile file('checkstyle.xml')
}

jacoco {
    toolVersion '0.7.1.201405082137'
}

jacocoTestReport {
    reports {
        html.enabled  true
        xml.enabled  true // needed for coveralls
    }
}

task wrapper(type: Wrapper) {
    gradleVersion '2.8'
}

tasks.cpd {
    reports {
        text.enabled true
        xml.enabled false
    }
}

tasks.withType(FindBugs) {
    reports {
        html.enabled true
        xml.enabled false
    }
}

tasks.jacocoTestReport {
    dependsOn 'test'    // skips after clean build otherwise
}

// release configuration
apply plugin: 'maven'
apply plugin: 'signing'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
        authentication(
            userName: hasProperty('ossrhUsername')
              ? ossrhUsername
              : '',
            password: hasProperty('ossrhPassword')
              ? ossrhPassword
              : '')
      }

      snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
        authentication(
            userName: hasProperty('ossrhUsername')
              ? ossrhUsername
              : '',
            password: hasProperty('ossrhPassword')
              ? ossrhPassword
              : '')
      }

      pom.project {
        name 'benjamin'
        packaging 'jar'
        description description
        url 'https://github.com/raindev/benjamin'

        scm {
          connection 'scm:git:git@github.com:raindev/benjamin.git'
          developerConnection 'scm:git:git@github.com:raindev/benjamin.git'
          url 'git@github.com:raindev/benjamin.git'
        }

        licenses {
          license {
            name 'The MIT License'
            url 'http://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id 'raindev'
            name 'Andrew Barchuk'
            email 'raindev@icloud.com'
          }
        }
      }
    }
  }
}
