apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'cpd'
apply plugin: 'checkstyle'

group = 'org.benjamin'
version = '0.1-SNAPSHOT'

description = 'Java Bencode minimalist library'

ext {
    pmdVersion = '5.2.3'
}

repositories {
     maven { url 'http://repo.maven.apache.org/maven2' }
}

dependencies {
    testCompile group: 'org.testng', name: 'testng', version:'6.8.13'
    pmd(
        'net.sourceforge.pmd:pmd-core:5.2.3',
        'net.sourceforge.pmd:pmd-java:5.2.3'
    )
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'de.aaschmid.gradle.plugins:gradle-cpd-plugin:0.1'
    }
}

test {
    useTestNG()

    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: " +
                    "${result.testCount} tests," +
                    " ${result.successfulTestCount} successful," +
                    " ${result.failedTestCount} failed," +
                    " ${result.skippedTestCount} skipped"
            }
        }
    }
}

findbugs {
    toolVersion = '3.0.0'
    effort = 'max'
    reportLevel = 'high'
}

pmd {
    toolVersion = pmdVersion
    ruleSetFiles = files('pmd.xml', 'pmd-main.xml')
}

cpd {
    toolVersion = pmdVersion
}

checkstyle {
    toolVersion = '6.2'
    configFile = file('checkstyle.xml')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

tasks.cpd {
    reports {
        text.enabled = true
        xml.enabled = false
    }
}

tasks.withType(FindBugs) {
    reports {
        html.enabled = true
        xml.enabled = false
    }
}
